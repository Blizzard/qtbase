#
# qmake configuration for winrt-arm-msvc2015
#
# Written for Microsoft Visual C++ 2015
#

include(../common/winrt_winphone/qmake.conf)
QMAKE_COMPILER_DEFINES += _MSC_VER=1900
DEFINES                += WINAPI_FAMILY=WINAPI_FAMILY_APP ARM __ARM__ __arm__

QMAKE_CFLAGS           += -FS
QMAKE_CXXFLAGS         += -FS
QMAKE_LFLAGS           += /MACHINE:ARM /NODEFAULTLIB:kernel32.lib

QMAKE_LIBS             += windowscodecs.lib WindowsApp.lib runtimeobject.lib

# Note that the order is important, ucrt(d) has to be first
# Otherwise the linker might use malloc from a different library
# but free_dbg() from the runtime, causing assert when deleting
# items from different heaps
CONFIG(debug, debug|release) {
    QMAKE_LIBS = ucrtd.lib $$QMAKE_LIBS
} else {
    QMAKE_LIBS = ucrt.lib $$QMAKE_LIBS
}

VCPROJ_ARCH             = ARM
MSVC_VER                = 14.0
WINSDK_VER              = 10.0
WINTARGET_VER           = winv10.0
WINRT_MANIFEST          = $$PWD/../common/winrt_winphone/manifests/10.0/AppxManifest.xml.in
WINRT_MANIFEST.architecture = arm
